apply plugin: 'build-dashboard'

subprojects {
    apply plugin: 'java'

    apply plugin: 'findbugs'
    apply plugin: 'jacoco'
    apply plugin: 'eclipse'
    
    group = "org.tweetwallfx"
    
    repositories {
        jcenter()
    }
    dependencies {
        //        compile fileTree(dir: 'libs', include: '*.jar')
        testCompile 'junit:junit:4.11'
    }
    version = '1.0'
    
    jar {
        manifest.attributes provider: 'gradle'
    }

    compileJava {
        options.compilerArgs << "-Xlint:all"// << "-Xlint:deprecation"
    } 

    findbugs {
        ignoreFailures = false
    }
    
    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }
}

project (":TweetWallFX-CommandlineArguments") {
    dependencies {
        compile group: 'org.netbeans.api', name: 'org-openide-util-lookup', version: 'RELEASE80'
        compile group: "com.beust", name: "jcommander", version: "1.48"
    }
}

project(":TweetWallFX-Tweet-API") {
    dependencies {
        compile group: 'org.netbeans.api', name: 'org-openide-util-lookup', version: 'RELEASE80'
    }
}

project(":TweetWallFX-Tweet-Impl-Twitter4J") {
    dependencies {
        compile group: "org.twitter4j", name: "twitter4j-core", version: "4.0.3"
        compile group: "org.twitter4j", name: "twitter4j-stream", version: "4.0.3"
        compile group: "com.beust", name: "jcommander", version: "1.48"
        compile project(':TweetWallFX-Tweet-API')
        compile project(':TweetWallFX-CommandlineArguments')
    }
}

project(":TweetWallFX-Core"){
    dependencies {
        compile group: "de.jensd", name: "fontawesomefx", version: "8.3"
        compile group: "log4j", name: "log4j", version: "1.2.17"
        compile project(':TweetWallFX-Tweet-API')
        compile project(':TweetWallFX-CommandlineArguments')
        compile project(':TweetWallFX-Tweet-Impl-Twitter4J')
    }
}

project(":TweetWallFX-2D"){
    dependencies {
        compile project(':TweetWallFX-Core')
        compile project(':TweetWallFX-Controls')
    }
}

project(":TweetWallFX-3D"){
    dependencies {
        compile fileTree(dir: 'libs', include: '*.jar')
        compile project(':TweetWallFX-Core')        
    }
}

project(":TweetWallFX-Controls"){
    dependencies {
        compile project(':TweetWallFX-Core')        
    }
}

project(":TweetWallFX-Devoxx"){
    
    apply plugin: "application"
    
    run {
        mainClassName = "org.tweetwallfx.devoxx.Main"        
        args = ["-oAuthFile", "MyOAuth.properties"]
    }
    
    task('debug', dependsOn: 'classes', type: JavaExec) {
        classpath = sourceSets.main.runtimeClasspath
        args = ["-oAuthFile", "MyOAuth.properties"]
        debug = true
    }    
    
    dependencies {
        compile project(':TweetWallFX-2D')        
        compile project(':TweetWallFX-Controls')        
    }
}

project(":TweetWallFX-NetBeans"){
    
    apply plugin: "application"
    
    run {
        mainClassName = "org.tweetwallfx.generic.Main"        
        args = [
            "-oAuthFile", "MyOAuth.properties",
            "-title", "The JavaFX Tweetwall for NetBeans Day!",
            "-query", "#netbeans OR #netbeansday",
            "-stylesheet", file('src/main/resources/netbeans.css').toURI().toURL().toString()
        ]
    }
    
    task('debug', dependsOn: 'classes', type: JavaExec) {
        main = "org.tweetwallfx.netbeans.Main"        
        classpath = sourceSets.main.runtimeClasspath
        args = [
            "-oAuthFile", "MyOAuth.properties",
            "-title", "The JavaFX Tweetwall for NetBeans Day!",
            "-query", "#netbeans OR #netbeansday",
            "-stylesheet", file('src/main/resources/netbeans.css').toURI().toURL().toString()
        ]
        debug = true
    }    
    
    dependencies {
        compile project(':TweetWallFX-Generic2D')
    }
}

project(":TweetWallFX-JavaOne"){
    dependencies {
        compile project(':TweetWallFX-2D')        
    }
}

project(":TweetWallFX-Generic2D"){
    dependencies {
        compile project(':TweetWallFX-2D')        
        compile project(':TweetWallFX-Controls')        
    }
}

description = 'TweetWallFX'

task ('runDevoxx', dependsOn: ':TweetWallFX-Devoxx:build',  type:(JavaExec)) {
    classpath = project(":TweetWallFX-Devoxx").sourceSets.main.runtimeClasspath
    main = "org.tweetwallfx.devoxx.DevoxxTweetWall"
    args = ["-oAuthFile", "MyOAuth.properties"]
}

task ('runNetBeans', dependsOn: ':TweetWallFX-NetBeans:build',  type:(JavaExec)) {
    classpath = project(":TweetWallFX-NetBeans").sourceSets.main.runtimeClasspath
    main = "org.tweetwallfx.netbeans.Main"
    args = ["-oAuthFile", "MyOAuth.properties"]
}
