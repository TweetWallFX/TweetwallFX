/*
 * The MIT License (MIT)
 *
 * Copyright (c) 2015-2022 TweetWallFX
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 */

plugins {
    id 'base'
    id 'build-dashboard'
    id 'com.diffplug.spotless' version '6.8.0'
    id 'com.github.ben-manes.versions' version '0.42.0'
    id 'com.github.spotbugs' version '5.0.9' apply false
    id 'com.google.osdetector' version '1.7.0'
    id 'nebula.info-dependencies' version '11.3.3' apply false
    id 'nebula.maven-nebula-publish' version '18.4.0' apply false
    id 'net.ltgt.errorprone' version '2.0.2' apply false
    id 'org.sonarqube' version '3.4.0.2513'
}

ext {
    javaFxPlatform = osdetector.os == 'osx' ? 'mac' : osdetector.os == 'windows' ? 'win' : osdetector.os
    javaFxPlatform = osdetector.arch == 'aarch_64' ? javaFxPlatform + '-aarch64' : javaFxPlatform
}

allprojects {
    group = 'org.tweetwallfx'
    version = currentVersion
    description = 'JavaFX based Tweetwall (' + name + ')'

    repositories {
        mavenCentral()
        jcenter()
    }

    // configuring Spotless
    apply plugin: 'com.diffplug.spotless'

    afterEvaluate {
        spotless {
            if (!System.env.CI) {
                // https://docs.github.com/en/actions/learn-github-actions/environment-variables#default-environment-variables
                // only format files which have changed since this ref
                // TODO: Figure out a way to get this working in CI (i.e. Github Workflows)
                ratchetFrom 'origin/master'
            }

            format 'textBasedFiles', {
                target(
                    '*.gradle',
                    '**/*.asciidoc',
                    '**/*.adoc',
                    '**/*.asc',
                    '**/*.css',
                    '**/*.json',
                    '**/*.md',
                    '**/*.properties',
                    '.gitignore',
                )

                trimTrailingWhitespace()
                indentWithSpaces(4)
                endWithNewline()
            }

            def closure = {
                trimTrailingWhitespace()
                indentWithSpaces(4)
                endWithNewline()
//                importOrder(
//                    '\\#',    // static imports
//                    'java',   // classes from java root package
//                    'javax',  // classes from javax root package
//                    '',       // any other import
//                )
                replaceRegex 'noStarImports', /\nimport (static )?[a-zA-Z0-9.]*[.][\*];\n/, '\n'
                replaceRegex 'noMultipleEmptyLines', '\n{3,}', '\n\n'
                replaceRegex 'Remove @author javadoc taglets.', '\n\\s*\\*\\s*@author.*', ''
                replaceRegex 'Remove trailing empty comment lines.', '\n\\s*\\*(\n\\s*\\*/\n)', '$1'
                replaceRegex 'Remove empty javadoc', '\n\\s*/\\*\\*\n\\s*\\*/\n', '\n\n'
                replaceRegex 'Remove empty lines before end of block', '\\n[\\n]+(\\s*})(?=\\n)', '\n$1'
                licenseHeaderFile rootProject.file('LICENSE.spotlessTemplate')
            }

            if (plugins.hasPlugin(GroovyPlugin)) {
                groovy(closure)
            } else if (plugins.hasPlugin(JavaPlugin)) {
                java(closure)
            }
        }

        tasks.named('check') {
            dependsOn spotlessCheck
        }
    }

    // now configuring for projects that contain code not just structuring the project
    if (childProjects.isEmpty()) {
        // configuring standard java project
        apply plugin: 'java-library'
        apply plugin: 'idea'
        apply plugin: 'jacoco'
        apply plugin: 'eclipse'
        apply plugin: 'nebula.info'
        apply plugin: 'maven-publish'

        jacoco {
            toolVersion = '0.8.8'
        }

        configurations {
            javafx {
                description = 'Java FX libraries'
            }
            jaxb

            all {
                resolutionStrategy.eachDependency { DependencyResolveDetails details ->
/*
                    if (details.requested.group == 'com.fasterxml.jackson' &&
                        details.requested.name == 'jackson-bom' &&
                        details.requested.version == '2.13.2.1') {
                        details.useVersion '2.13.2.20220324'
                        details.because 'fixes CVE-2020-36518 and resolved version does not (pom) match expectation'
                    }
*/
                }
            }
            testFramework {
                description = 'Libraries for testing'
                // exclude old junit api
                exclude group: 'junit'
            }
            testImplementation.extendsFrom testFramework
        }

        dependencies {
            javafx 'org.openjfx:javafx-base:18.0.1:' + javaFxPlatform
            javafx 'org.openjfx:javafx-graphics:18.0.1:' + javaFxPlatform
            javafx 'org.openjfx:javafx-fxml:18.0.1:' + javaFxPlatform
            javafx 'org.openjfx:javafx-controls:18.0.1:' + javaFxPlatform

            jaxb 'jakarta.activation:jakarta.activation-api:2.1.0'
            jaxb 'org.eclipse.angus:angus-activation:1.0.0'
            jaxb 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.0'
            jaxb 'org.glassfish.jaxb:jaxb-runtime:4.0.0'

            testFramework 'org.junit.jupiter:junit-jupiter-api:5.8.2'
            testFramework 'org.junit.jupiter:junit-jupiter-params:5.8.2'
            testFramework 'org.assertj:assertj-core:3.23.1'
            testFramework 'org.mockito:mockito-junit-jupiter:4.6.1'

            testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
            testRuntimeOnly 'org.junit.platform:junit-platform-runner:1.8.2'
            testRuntimeOnly 'org.mockito:mockito-inline:4.6.1'
        }

        ext {
            getJvmModulePath = { -> (configurations.javafx + configurations.jaxb).asPath}
            getJvmAdditionalModules = { ->
                [
                    'jakarta.activation',
                    'jakarta.xml.bind',
                    'javafx.controls',
                    'javafx.graphics',
                    'javafx.fxml'
                ].toUnique().toSorted().join(',')
            }
        }

        tasks.named('jar') {
            manifest.attributes provider: 'gradle'
        }

        tasks.named('javadoc') {
            options.addStringOption('-module-path', getJvmModulePath())
            options.addStringOption('-add-modules', getJvmAdditionalModules())
            enabled = false // TODO: re-enable (currently breaks build with ClassCastException)
        }

        tasks.withType(JavaCompile).configureEach {
            options.compilerArgs.addAll([
                    '-Xlint:-preview',
                    '--enable-preview',
                    '-Werror',
                    '-proc:none',
                    '--module-path',
                    getJvmModulePath(),
                    '--add-modules',
                    getJvmAdditionalModules(),
            ])
        }

        tasks.withType(JavaExec).configureEach {
            jvmArgs '--enable-preview'
            jvmArgs '--module-path'
            jvmArgs getJvmModulePath()
            jvmArgs '--add-modules'
            jvmArgs getJvmAdditionalModules()
        }

        tasks.withType(Test).configureEach {
            jvmArgs '--enable-preview'
            jvmArgs '--module-path'
            jvmArgs getJvmModulePath()
            jvmArgs '--add-modules'
            jvmArgs getJvmAdditionalModules()

            useJUnitPlatform()
            testLogging {
                events 'skipped', 'passed', 'failed'
            }
        }

        tasks.named('check') {
            dependsOn javadoc
        }

        if (!JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_18)) {
            // jacoco not yet compatible with JDK18
            tasks.named('test') {
                finalizedBy jacocoTestReport
            }
        }

        // configuring Errorprone
        apply plugin: 'net.ltgt.errorprone'

        dependencies {
            errorprone 'com.google.errorprone:error_prone_core:2.14.0'
        }

        // configuring Spotbugs
        apply plugin: 'com.github.spotbugs'

        tasks.withType(com.github.spotbugs.snom.SpotBugsTask).configureEach {
            // fail build in case a failure was detected
            ignoreFailures = false

            jvmArgs.add '--module-path'
            jvmArgs.add getJvmModulePath()
            jvmArgs.add '--add-modules'
            jvmArgs.add getJvmAdditionalModules()

            excludeFilter = rootProject.file('spotbugs-exclude.xml')

            reports {
                xml.required.set(false)
                html.required.set(true)
            }

            auxClassPaths = auxClassPaths + configurations.javafx + configurations.jaxb
        }

        // configuring Versions plugin
        tasks.withType(com.github.benmanes.gradle.versions.updates.DependencyUpdatesTask).configureEach {
            outputFormatter = 'plain,html'
            gradleReleaseChannel = 'current'

            resolutionStrategy {
                def rejectedQualifierParts = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'pr', 'ea']
                componentSelection { rules ->
                    rules.all { ComponentSelection selection ->
                        boolean rejected = rejectedQualifierParts.any { qualifier ->
                            selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-+]*/
                        }
                        if (rejected) {
                            selection.reject('Release candidate')
                        }
                    }
                }
            }
        }

        plugins.withType(GroovyPlugin) {
            dependencies {
                implementation 'org.codehaus.groovy:groovy-all:4.0.2'
                testFramework 'org.spockframework:spock-core:2.1-groovy-3.0'
            }
        }

        tasks.withType(Test).configureEach {
            systemProperty 'org.tweetwallfx.tests.executeCFPClientLiveTests', executeCFPClientLiveTests
        }

        apply plugin: 'nebula.maven-publish'
        apply plugin: 'nebula.javadoc-jar'
        apply plugin: 'nebula.source-jar'
        apply plugin: 'nebula.publish-verification'
    }
}

tasks.register('createFolders') {
    description = 'Creates the folders for the projects and sourceSets should they not exist.'

    rootProject.allprojects.each {p ->
        if (!p.getProjectDir().isDirectory()) {
            logger.lifecycle 'Creating project folder: {}', p.getProjectDir()
            p.getProjectDir().mkdirs()
        }

        if (p.hasProperty('sourceSets')) {
            p.sourceSets*.allSource*.srcDirs*.each { File srcDir ->
                if (!srcDir.isDirectory()) {
                    logger.lifecycle 'Creating source folder: {}', srcDir
                    srcDir.mkdirs()
                }
            }
        }
    }
}
